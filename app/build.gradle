/*
 * Copyright 2018 Google LLC.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

def nodebug = hasProperty("nodebug")

apply from: "${rootProject.projectDir}/flavors.gradle"

android {
    buildFeatures.dataBinding = true
    compileSdkVersion versions.compileSdk

    defaultConfig {
        applicationId names.applicationId
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        debug {
            debuggable !nodebug
        }

        release {
            minifyEnabled true
        }

        staging {
            minifyEnabled true
            matchingFallbacks = ['release']
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    dynamicFeatures = [':features:kotlin',
                       ':features:initialInstall',
                       ':features:java',
//                       ':features:native',
                       ':features:maxSdk',
                       ':features:assets',
                       ':instant:split',
                       ':instant:url'
    ]
}

dependencies {

    // Libraries which can be re-used in other modules should use the `api` keyword.
    // This way they can be shared with dependent feature modules.

    implementation 'androidx.annotation:annotation:1.2.0'

    implementation 'androidx.multidex:multidex:2.0.1'

    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"
    api "androidx.appcompat:appcompat:${versions.appcompat}"
    api 'androidx.constraintlayout:constraintlayout:2.1.0'
    api 'com.google.android.material:material:1.4.0'
    api "com.google.android.play:core:${versions.playcore}"

    androidTestImplementation "androidx.test.espresso:espresso-contrib:${versions.espresso}"
    androidTestImplementation "androidx.test:rules:${versions.testRules}"
    androidTestImplementation "androidx.test:runner:${versions.testRunner}"
    androidTestImplementation "androidx.test.ext:junit:${versions.extJunit}"

    // When using API in base, some dependencies might have to be re-added for test implementation.
    androidTestImplementation "androidx.appcompat:appcompat:${versions.appcompat}"
}
